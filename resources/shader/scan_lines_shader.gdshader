shader_type canvas_item;
render_mode unshaded, skip_vertex_transform;



const float curvature = 8.0;
const float vignette_multiplier = 0.1;

const float scanline_width = 180.0;
const float scanline_width_multiplyer = 0.2;
const float scanline_curve_multiplyer = 2.0;

uniform sampler2D screen_texture: hint_screen_texture, filter_linear_mipmap;

void fragment() {
	vec2 centered_uv = SCREEN_UV * 2.0 - 1.0;
	vec2 uv_offset = centered_uv.yx / curvature;
	vec2 warped_uv = centered_uv + centered_uv * uv_offset * uv_offset;
	vec3 cutoff = vec3(step(abs(warped_uv.x), 1.0) * step(abs(warped_uv.y), 1.0));
	vec3 scanlines = vec3(sin(scanline_curve_multiplyer * warped_uv.y * scanline_width) * scanline_width_multiplyer + 1.2);
	vec3 vignette = vec3(length(pow(abs(centered_uv), vec2(4.0)) / 3.0));

	vec3 screen_color = textureLod(screen_texture, (warped_uv + 1.0) / 2.0, 0.2).rgb * cutoff * scanlines;
	screen_color -= vignette * vignette_multiplier;
	COLOR = vec4(screen_color, 1.0);
}